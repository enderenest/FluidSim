#version 430 core
layout(local_size_x = 32) in;

struct Entry { int index; uint key; };

layout(std430, binding = 6) buffer SpatialLookup {
    Entry spatialLookup[];
};

uniform uint u_N;       // real element count (1024)
uniform uint u_size;    // subarray length: 2,4,8,…,1024
uniform uint u_stride;  // merge stride: size/2, size/4, …, 1

void main() {
    uint idx     = gl_GlobalInvocationID.x;
    uint partner = idx ^ u_stride;

   // bitonic_sort.comp
    if (idx < u_N && partner < u_N && idx < partner) {
        bool ascending = ((idx & u_size) == 0u);
        Entry a = spatialLookup[idx];
        Entry b = spatialLookup[partner];
        if ((a.key > b.key) == ascending) {
            spatialLookup[idx]     = b;
            spatialLookup[partner] = a;
        }
    }
}